include(CMakeParseArguments)

if(BASJOO_BUILD_TESTING)
  include(CTest)
  enable_testing()
endif()

if(NOT DEFINED BASJOO_IDE_FOLDER)
  set(BASJOO_IDE_FOLDER ${CMAKE_PROJECT_NAME})
endif()

function(basjoo_cxx_library)
  cmake_parse_arguments(BASJOO_CXX_LIB
    "DISABLE_INSTALL;PUBLIC;TESTONLY"
    "NAME"
    "HDRS;SRCS;COPTS;DEFINES;LINKOPTS;DEPS"
    ${ARGN}
  )

  set(_SRCS "${BASJOO_CXX_LIB_SRCS}")
  list(FILTER _SRCS EXCLUDE REGEX ".*\\.(h|hpp|inc)")
  if(_SRCS STREQUAL "")
    set(_IS_INTERFACE 1)
  else()
    set(_IS_INTERFACE 0)
  endif()
  unset(_SRCS)

  if(NOT _IS_INTERFACE)
    add_library(${BASJOO_CXX_LIB_NAME} "")
    set_property(TARGET ${BASJOO_CXX_LIB_NAME} PROPERTY LINKER_LANGUAGE "CXX")
    target_sources(${BASJOO_CXX_LIB_NAME}
      PRIVATE
        ${BASJOO_CXX_LIB_SRCS}
      PUBLIC
        FILE_SET HEADERS
          TYPE HEADERS
          BASE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}
          FILES ${BASJOO_CXX_LIB_HDRS}
    )
    target_include_directories(${BASJOO_CXX_LIB_NAME}
      PUBLIC
        "$<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/src>"
        "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>"
    )
    target_compile_options(${BASJOO_CXX_LIB_NAME} PRIVATE ${BASJOO_CXX_LIB_COPTS})
    target_compile_definitions(${BASJOO_CXX_LIB_NAME} PUBLIC ${BASJOO_CXX_LIB_DEFINES})
    target_link_options(${BASJOO_CXX_LIB_NAME} PRIVATE ${BASJOO_CXX_LIB_LINKOPTS})
    target_link_libraries(${BASJOO_CXX_LIB_NAME} PUBLIC ${BASJOO_CXX_LIB_DEPS})

    if(BASJOO_CXX_LIB_PUBLIC)
      set_property(TARGET ${BASJOO_CXX_LIB_NAME} PROPERTY FOLDER ${BASJOO_IDE_FOLDER})
    elseif(BASJOO_CXX_LIB_TESTONLY)
      set_property(TARGET ${BASJOO_CXX_LIB_NAME} PROPERTY FOLDER ${BASJOO_IDE_FOLDER}/tests)
    else()
      set_property(TARGET ${BASJOO_CXX_LIB_NAME} PROPERTY FOLDER ${BASJOO_IDE_FOLDER}/internal)
    endif()
    
    if(BASJOO_ENABLE_INSTALL)
      set_target_properties(${BASJOO_CXX_LIB_NAME} PROPERTIES
        OUTPUT_NAME "basjoo_${BASJOO_CXX_LIB_NAME}"
        SOVERSION "${BASJOO_SOVERSION}"
      )
    endif()

  else()
    add_library(${BASJOO_CXX_LIB_NAME} INTERFACE)
    target_sources(${BASJOO_CXX_LIB_NAME}
      INTERFACE
        FILE_SET HEADERS
          TYPE HEADERS
          BASE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}
          FILES ${BASJOO_CXX_LIB_HDRS}
    )
    target_include_directories(${BASJOO_CXX_LIB_NAME}
      INTERFACE
        "$<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/src>"
        "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>"
    )
    target_compile_definitions(${BASJOO_CXX_LIB_NAME} INTERFACE ${BASJOO_CXX_LIB_DEFINES})
    target_link_options(${BASJOO_CXX_LIB_NAME} INTERFACE ${BASJOO_CXX_LIB_LINKOPTS})
    target_link_libraries(${BASJOO_CXX_LIB_NAME} INTERFACE ${BASJOO_CXX_LIB_DEPS})

  endif()

  unset(_IS_INTERFACE)

  if(BASJOO_ENABLE_INSTALL)
    file(RELATIVE_PATH _REL_PATH "${PROJECT_SOURCE_DIR}/src" "${CMAKE_CURRENT_SOURCE_DIR}")
    install(TARGETS ${BASJOO_CXX_LIB_NAME} EXPORT ${CMAKE_PROJECT_NAME}Targets
      FILE_SET HEADERS DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${_REL_PATH}
      RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
      LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
      ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    )
    unset(_REL_PATH)
  endif()

  add_library(${CMAKE_PROJECT_NAME}::${BASJOO_CXX_LIB_NAME} ALIAS ${BASJOO_CXX_LIB_NAME})

endfunction()

function(basjoo_cxx_module)
  cmake_parse_arguments(BASJOO_CXX_MODULE
    "DISABLE_INSTALL;PUBLIC;TESTONLY"
    "NAME"
    "IXXS;SRCS;COPTS;DEFINES;LINKOPTS;DEPS"
    ${ARGN}
  )

  add_library(${BASJOO_CXX_MODULE_NAME} "")
  target_sources(${BASJOO_CXX_MODULE_NAME}
    PUBLIC
      FILE_SET CXX_MODULES
        TYPE CXX_MODULES
        BASE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}
        FILES ${BASJOO_CXX_MODULE_IXXS}
  )
  target_include_directories(${BASJOO_CXX_MODULE_NAME}
    PUBLIC
      "$<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/src>"
      "$<INSTALL_INTERFACE:${CMAKE_INSTALL_DIR}/modules>"
  )
  target_compile_options(${BASJOO_CXX_MODULE_NAME} PRIVATE ${BASJOO_CXX_MODULE_COPTS})
  target_compile_definitions(${BASJOO_CXX_MODULE_NAME} PUBLIC ${BASJOO_CXX_MODULE_DEFINES})
  target_link_options(${BASJOO_CXX_MODULE_NAME} PRIVATE ${BASJOO_CXX_MODULE_LINKOPTS})
  target_link_libraries(${BASJOO_CXX_MODULE_NAME} PUBLIC ${BASJOO_CXX_MODULE_DEPS})

  if(BASJOO_ENABLE_INSTALL)
    file(RELATIVE_PATH _REL_PATH "${PROJECT_SOURCE_DIR}/src" "${CMAKE_CURRENT_SOURCE_DIR}")
    install(TARGETS ${BASJOO_CXX_MODULE_NAME} EXPORT ${CMAKE_PROJECT_NAME}Targets
      FILE_SET CXX_MODULES DESTINATION ${CMAKE_INSTALL_PREFIX}/modules/${_REL_PATH}
      RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
      LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
      ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    )
    unset(_REL_PATH)
  endif()

  add_library(${CMAKE_PROJECT_NAME}::${BASJOO_CXX_MODULE_NAME} ALIAS ${BASJOO_CXX_MODULE_NAME})
endfunction()

function(basjoo_cxx_test)
  if(NOT (BUILD_TESTING AND BASJOO_BUILD_TESTING))
    return()
  endif()

  cmake_parse_arguments(BASJOO_CXX_TEST
    ""
    "NAME"
    "SRCS;COPTS;DEFINES;LINKOPTS;DEPS"
    ${ARGN}
  )

  add_executable(${BASJOO_CXX_TEST_NAME} ${BASJOO_CXX_TEST_SRCS})
  set_property(TARGET ${BASJOO_CXX_TEST_NAME} PROPERTY FOLDER ${BASJOO_IDE_FOLDER}/tests)
  target_include_directories(${BASJOO_CXX_TEST_NAME}
    PRIVATE
      "$<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/src>"
      "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>"
  )
  target_compile_options(${BASJOO_CXX_TEST_NAME} PRIVATE ${BASJOO_CXX_TEST_COPTS})
  target_compile_definitions(${BASJOO_CXX_TEST_NAME} PRIVATE ${BASJOO_CXX_TEST_DEFINES})
  target_link_options(${BASJOO_CXX_TEST_NAME} PRIVATE ${BASJOO_CXX_TEST_LINKOPTS})
  target_link_libraries(${BASJOO_CXX_TEST_NAME}
    PRIVATE
      cxxopts::cxxopts
      doctest::doctest
      Matplot++::matplot
      ${BASJOO_CXX_TEST_DEPS}
  )

  add_test(NAME ${BASJOO_CXX_TEST_NAME} COMMAND ${BASJOO_CXX_TEST_NAME})
endfunction()
